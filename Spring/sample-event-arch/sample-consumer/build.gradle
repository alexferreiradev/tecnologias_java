plugins {
	id 'org.springframework.boot' version '2.5.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'idea'
}

group = 'dev.alexferreira'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.13.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
}

test {
	useJUnitPlatform()
}

sourceSets {
    intTest {
		java {
			compileClasspath += sourceSets.main.output + sourceSets.test.output
			runtimeClasspath += sourceSets.main.output + sourceSets.test.output
			srcDir 'src/integration-test/java'
		}
		resources {
			srcDir 'src/integration-test/resources'
		}
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
    intTestRuntimeOnly.extendsFrom testRuntimeOnly

	// Necessario para plugin idea
	intTestImplementation.canBeResolved true
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	useJUnitPlatform()
	shouldRunAfter test
}

// Ative para CI, mas não para dev, pois são testes lentos e dependentes de infra local
//check.dependsOn integrationTest

idea {
	module {
		testSourceDirs += sourceSets.intTest.java.srcDirs
		testResourceDirs += sourceSets.intTest.resources.srcDirs
		scopes.TEST.plus += [configurations.intTestImplementation ]
	}
}